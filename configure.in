dnl Process this file with autoconf to produce a configure script.
dnl old autoconf
AC_INIT(base/Engine.cpp)
dnl AC_INIT(pinball, 0.0.8, [henqvist@excite.com])
dnl AC_CONFIG_SRCDIR(base/Engine.cpp)

dnl this is a c++ program
CFLAGS="-g -W -Wall -pedantic"
CXXFLAGS="-g -W -Wall -pedantic"

SDL_VERSION=1.2.0
ALLEGRO_VERSION=4.0.0

AC_CANONICAL_HOST
AC_CANONICAL_TARGET

AC_CONFIG_SUBDIRS(libltdl)

dnl old automake?
AM_INIT_AUTOMAKE(pinball, 0.0.8)
dnl AM_INIT_AUTOMAKE
AM_CONFIG_HEADER(config.h)

dnl Checks for programs.
AC_PROG_CXX
AC_PROG_INSTALL
dnl AC_PROG_RANLIB
dnl libtool fucked things up
AC_LIBTOOL_DLOPEN
AC_LIBLTDL_CONVENIENCE
AC_PROG_LIBTOOL

AC_SUBST(INCLTDL)
AC_SUBST(LIBLTDL)

dnl *******************************************
dnl PATHS AND DIRS ****************************
dnl data dirs, fuck I hate autoconf, spent 4 hours getting this right had to steal it from 
dnl xine and even that took a couple of hours
if test "x$prefix" = xNONE; then
  prefix="${ac_default_prefix}"
fi

EM_DATADIR="$datadir/$PACKAGE"
eval EM_DATAPATH=`eval echo "$EM_DATADIR"`
AC_DEFINE_UNQUOTED(EM_DATADIR, "$EM_DATAPATH")
AC_SUBST(EM_DATAPATH)
AC_SUBST(EM_DATADIR)

dnl *******************************************
dnl Allegro or SDL version ********************

AC_ARG_WITH(allegro, [  --with-allegro          use allegro (software rendering) instead of SDL and OpenGL], use_allegro="yes", use_allegro="no")

if test x"$use_allegro" == "xno"; then

AC_DEFINE(EM_USE_SDL)

dnl *******************************************
dnl OPENGL ************************************
dnl Figure out which math and GL library to use
case "$target" in
    *-*-cygwin* | *-*-mingw32*)
        MATHLIB=""
        SYS_GL_LIBS="-lopengl32"
        ;;	
    *-*-beos*)
        MATHLIB=""
        SYS_GL_LIBS="-lGL"
        ;;
    *-*-darwin*)
        MATHLIB=""
        SYS_GL_LIBS=""
        ;;
    *-*-aix*)
        if test x$ac_cv_prog_gcc = xyes; then
            CFLAGS="-mthreads"
				fi
        SYS_GL_LIBS=""
        ;;
    *)
        MATHLIB="-lm"
        AC_PATH_X
        AC_PATH_XTRA
        if test x$have_x = xyes; then
            CFLAGS="$CFLAGS $X_CFLAGS"
            SYS_GL_LIBS="$X_PRE_LIBS $X_LIBS $X_EXTRA_LIBS -lGL -lGLU"
        else
            SYS_GL_LIBS="-lGL -lGLU"
        fi
        ;;
esac

AC_SUBST(MATHLIB)

dnl Check for OpenGL
AC_MSG_CHECKING(for OpenGL support)
AC_TRY_COMPILE([
 #if defined(__APPLE__) && defined(__MACH__)
 #include <OpenGL/gl.h>
  #else
 #include <GL/gl.h>
 #endif
], , , AC_MSG_ERROR([*** OpenGL not found on system!]))
AC_MSG_RESULT(yes)
GL_LIBS="$SYS_GL_LIBS"
AC_SUBST(GL_LIBS)
LIBS="$GL_LIBS $LIBS"

dnl *******************************************
dnl SDL ***************************************
AM_PATH_SDL($SDL_VERSION, : , AC_MSG_ERROR([*** SDL version $SDL_VERSION not found! Make sure you have the development package of SDL installed - (www.libsdl.org) - or try to use the --with-sdl-prefix option]))
CFLAGS="$CFLAGS $SDL_CFLAGS"
CPPFLAGS="$CPPFLAGS $SDL_CFLAGS"
LIBS="$LIBS $SDL_LIBS"

dnl SDL_image *********************************
AC_ARG_WITH(sdlimage-prefix, [  --with-sdlimage-prefix=PFX prefix where SDL_image library is installed], sdlimage_prefix="$withval", sdlimage_prefix="")
 
if test x"$sdlimage_prefix" != "x"; then
	SDLIMAGE_LIB="-L$sdlimage_prefix/lib"
	SDLIMAGE_CFLAGS="-I$sdlimage_prefix/include"
	LDFLAGS="$SDLIMAGE_LIB $LDFLAGS"
	CPPFLAGS="$CPPFLAGS $SDLIMAGE_CFLAGS"
	CFLAGS="$CFLAGS $SDLIMAGE_CFLAGS"
fi

AC_CHECK_LIB(SDL_image, IMG_Load, : ,AC_MSG_ERROR([*** libSDL_image not found! Make sure you have the development package of SDL_image installed - (www.libsdl.org) - or try to use the --with-sdlimage-prefix option]), )
AC_DEFINE(EM_USE_SDLIMAGE)
LIBS="$LIBS -lSDL_image"

dnl SDL_mixer **********************************
AC_ARG_WITH(sdlmixer-prefix, [  --with-sdlmixer-prefix=PFX prefix where SDL_mixer library is installed], sdlmixer_prefix="$withval", sdlmixer_prefix="")

if test x"$sdlmixer_prefix" != "x"; then
	SDLMIXER_LIB="-L$sdlmixer_prefix/lib"
	SDLMIXER_CFLAGS="-I$sdlmixer_prefix/include"
	LDFLAGS="$SDLMIXER_LIB $LDFLAGS"
	CPPLAGS="$CPPFLAGS $SDLMIXER_CFLAGS"
	CFLAGS="$CFLAGS $SDLMIXER_CFLAGS"
fi

AC_CHECK_LIB(SDL_mixer, Mix_OpenAudio, : ,AC_MSG_ERROR([*** libSDL_mixer not found. Make sure you have the development package of SDL_mixer installed - (www.libsdl.org) - or try to use --with-sdlmixer-prefix option]), )
AC_DEFINE(EM_USE_SDLMIXER)
LIBS="$LIBS -lSDL_mixer"

dnl else use allegro instead of sdl

else

AC_DEFINE(EM_USE_ALLEGRO)

dnl *******************************************
dnl ALLEGRO ***********************************
AM_PATH_ALLEGRO($ALLEGRO_VERSION, : , AC_MSG_ERROR([*** Allegro version $ALLEGRO_VERSION not found! Make sure you have the development package of Allegro installed - (www.talula.demon.co.uk/allegro) - or try to use the --with-allegro-prefix option]) )
CFLAGS="$CFLAGS $ALLEGRO_CFLAGS"
CPPFLAGS="$CPPFLAGS $ALLEGRO_CFLAGS"
LIBS="$LIBS $ALLEGRO_LIBS"

fi 

dnl ********************************************
dnl CPP_UNIT ***********************************
AM_PATH_CPPUNIT(1.8.0, [
	AC_DEFINE(EM_UNIT_TEST) 
	CPPFLAGS="$CPPFLAGS $CPPUNIT_CFLAGS"	
	CFLAGS="$CFLAGS $CPPUNIT_CFLAGS" 
	LIBS="$LIBS $CPPUNIT_LIBS"
	], AC_MSG_WARN([*** CppUnit was not found. No unit testing.]))

dnl ********************************************
dnl HEADERS ************************************
dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(sys/stat.h, : ,AC_MSG_ERROR([*** Header sys/stat.h not found. Not able to save config]))
AC_CHECK_HEADERS(sys/types.h, : ,AC_MSG_ERROR([*** Header sys/types.h not found. Not able to save config]))

dnl ********************************************
dnl Debug and Config ***************************
AC_DEFINE(EM_DEBUG)
AC_DEFINE(EM_USE_SOURCE_SPECULAR)
dnl AC_DEFINE(EM_USE_FAST_FLOAT2INT)
dnl AC_DEFINE(EM_DEBUG_COLLISION)
AC_DEFINE(EM_USE_QUADTREE)
AC_DEFINE(EM_USE_SHARED_COLOR)

AC_OUTPUT(Makefile addon/Makefile base/Makefile data/Makefile data/tux/Makefile data/professor/Makefile src/Makefile test/Makefile)
